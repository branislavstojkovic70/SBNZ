package backward;

import com.ftn.sbnz.model.models.Fact;
import com.ftn.sbnz.model.models.Result;
import com.ftn.sbnz.model.models.users.Patient;
import com.ftn.sbnz.model.models.users.OperatedPatient;
import com.ftn.sbnz.model.models.examinations.Examination;
import com.ftn.sbnz.model.models.examinations.ExaminationState;
import com.ftn.sbnz.model.models.examinations.Symptom;
import com.ftn.sbnz.model.models.examinations.SymptomFrequency;
import com.ftn.sbnz.model.models.examinations.ExaminationType;
import com.ftn.sbnz.model.models.examinations.TumorType;
import com.ftn.sbnz.model.models.examinations.TestResult;
import com.ftn.sbnz.model.models.examinations.Diagnosis;
import com.ftn.sbnz.model.models.examinations.TNMKlassification;
import com.ftn.sbnz.model.models.alarms.Alarm;
import java.time.LocalDateTime;
import java.util.HashSet;

global Result result;

query childOf(Patient child, Patient parent)
    Fact(child, parent;)
    or
    (Fact(intermediate, parent;) and childOf(child, intermediate;))
end

rule "print all facts"
when
    childOf($child, $parent;)
then
    result.addFactFromPatients($child, $parent);
end

rule "warn children of tumor diagnosis"
when
    $parent : Patient()
    $examination : Examination(diagnosis.isTumorDetected == true) from $parent.getExaminations()
    childOf($child, $parent;)
then
    insert(new Alarm(null, $child, "Parent has tumor", LocalDateTime.now()));
    result.addFact("Warning: " + $child.getIme() + " should schedule a screening due to parent's tumor diagnosis");
end

rule "Schedule examination due to tumor alarm"
when
    $alarm : Alarm(description == "Parent has tumor", $patient : patient)
then
    Examination newExamination = new Examination(null, LocalDateTime.now().plusMonths(1), null, "Detected tumor at parent",
            ExaminationState.SCHEDULED, new HashSet<ExaminationType>(), new HashSet<Symptom>(),
            null, null);
    insert(newExamination);
    System.out.println("Scheduled a new examination for patient: " + $patient.getIme() + " due to parental tumor diagnosis.");
    result.addFact("Scheduled examination for patient: " + $patient.getIme() + " due to parental tumor diagnosis.");
end
