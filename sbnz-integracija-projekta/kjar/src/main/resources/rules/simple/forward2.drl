package simple;

import com.ftn.sbnz.model.models.users.Patient;
import com.ftn.sbnz.model.models.users.OperatedPatient;
import com.ftn.sbnz.model.models.examinations.Examination;
import com.ftn.sbnz.model.models.examinations.ExaminationState;
import com.ftn.sbnz.model.models.examinations.TestResult;
import com.ftn.sbnz.model.models.examinations.ExaminationType;
import com.ftn.sbnz.model.models.therapy.Operation;
import com.ftn.sbnz.model.models.therapy.OperationType;
import com.ftn.sbnz.model.models.therapy.PaliativeCare;
import com.ftn.sbnz.model.models.therapy.Therapy;
import com.ftn.sbnz.model.models.therapy.TherapyState;
import com.ftn.sbnz.model.models.therapy.TherapyType;

import java.time.LocalDateTime;
import java.util.Set;

rule "Create OperatedPatient"
when
    $patient : Patient($examinations: examinations)
    $examination : Examination(examinationState == ExaminationState.DONE, therapy != null) from $examinations
    $operation : Operation() from $examination.getTherapy()
then
    OperatedPatient operatedPatient = new OperatedPatient($patient, $operation, false, false, "OperatedPatient created for patient: " + $patient.getId(), null);
    insert(operatedPatient);
    System.out.println("OperatedPatient created for patient: " + $patient.getId());
    System.out.println("Okinulo se forward 2 pravilo 1");
end

rule "Check for resistance in pulmonary blood vessels"
when
    $operatedPatient : OperatedPatient()
    $patient : Patient($examinations: examinations) from $operatedPatient.getPatient()
    $ultrasound : Examination(note == "ultrasound", examinationState == ExaminationState.DONE) from $examinations
    $ultrasoundType : ExaminationType() from $ultrasound.getExaminationTypes()
    $ultrasoundResult : TestResult(description == "pulmonary artery pressure", value > 25.0) from $ultrasoundType.getTestResults()
    $spiroergometry : Examination(note == "spiroergometry", examinationState == ExaminationState.DONE) from $examinations
    $spiroergometryType : ExaminationType() from $spiroergometry.getExaminationTypes()
    $spiroergometryResult : TestResult(description == "VO2 max", value < 80.0) from $spiroergometryType.getTestResults()
then
    modify($operatedPatient) {
        setPulmonaryResistance(true);
    }
    System.out.println("Resistance in pulmonary blood vessels detected for patient: " + $patient.getId());
    System.out.println("Okinulo se forward 2 pravilo 2");
end

rule "Diagnose pulmonary hypertension"
when
    $operatedPatient : OperatedPatient(pulmonaryResistance == true)
then
    modify($operatedPatient) {
        setPulmonaryHypertension(true);
    }
    System.out.println("Pulmonary hypertension diagnosed for patient: " + $operatedPatient.getPatient().getId());
    System.out.println("Okinulo se forward 2 pravilo 3");
end

rule "Recommend lifestyle changes and palliative care"
when
    $operatedPatient : OperatedPatient(pulmonaryHypertension == true)
then
    PaliativeCare therapy = new PaliativeCare(null, LocalDateTime.now(), LocalDateTime.now().plusMonths(6), 
            "Recommended palliative care for pulmonary hypertension.", TherapyType.PALLIATIVE_CARE,
            TherapyState.DURING, "Recommended palliative care for pulmonary hypertension.", 
            "Medication");
    modify($operatedPatient) {
        setNote("Recommend lifestyle changes for patient: " + $operatedPatient.getPatient().getId()),
        setFurtherTherapy(therapy)
    };
    System.out.println("Okinulo se forward 2 pravilo 4");
end


rule "Recommend heart and lung transplant"
when
    $operatedPatient : OperatedPatient(pulmonaryHypertension == true)
    $patient : Patient($examinations: examinations) from $operatedPatient.getPatient()
    $examination : Examination(therapy != null) from $examinations
    $therapy : Therapy(therapyState == TherapyState.FINISHED, therapyType == TherapyType.PALLIATIVE_CARE) from $examination.getTherapy()
then
    Operation transplantTherapy = new Operation(null, LocalDateTime.now(), LocalDateTime.now().plusYears(1), 
        "Recommended heart and lung transplant for advanced pulmonary hypertension.", TherapyType.OPERATION, 
        TherapyState.PLANNED, "Recommended heart and lung transplant for advanced pulmonary hypertension.", 
        LocalDateTime.now().plusYears(1), "", OperationType.SEGMENTECTOMY);
    modify($operatedPatient){
        setFurtherTherapy(transplantTherapy);
    }
    System.out.println("Okinulo se forward 2 pravilo 5");
end